//For now, you have to manually edit the resulting mixin.refmap.json file to include the proper paths to the api mixins
buildscript
{
	repositories
	{
		jcenter()
		maven { url = "https://files.minecraftforge.net/maven" }
		maven
		{
			name = 'sponge'
			url = 'https://repo.spongepowered.org/repository/maven-public'
		}
	}
	dependencies
	{
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
	}
}

apply plugin: 'eclipse'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
project.mkdir "libs"

version = mod_version
group = mod_group
archivesBaseName = mod_name

minecraft
{
	version = "${minecraft_version}-${forge_version}"
	mappings = minecraft_mappings
	makeObfSourceJar = false
	runDir = "run"
	clientRunArgs = ["--tweakClass", "org.spongepowered.asm.launch.MixinTweaker", "--mixin", "META-INF/${mod_id}_mixin.json"]
	serverRunArgs = ["--tweakClass", "org.spongepowered.asm.launch.MixinTweaker", "--mixin", "META-INF/${mod_id}_mixin.json"]
}
runClient.args = ["--tweakClass", "org.spongepowered.asm.launch.MixinTweaker", "--mixin", "META-INF/${mod_id}_mixin.json"]
runServer.args = ["--tweakClass", "org.spongepowered.asm.launch.MixinTweaker", "--mixin", "META-INF/${mod_id}_mixin.json"]

repositories
{
	maven
	{
		name 'Progwml6'
		url "http://dvs1.progwml6.com/files/maven"
	}
	maven {
		name = 'SpongePowered'
		url = 'https://repo.spongepowered.org/repository/maven-public/'
   	}
}

dependencies
{
   runtime "mezz.jei:jei_${minecraft_version}:${jei_version}"
   compile ("org.spongepowered:mixin:${mixin_version}")
   {
       exclude module: "launchwrapper"
       exclude module: "guava"
       exclude module: "gson"
       exclude module: "commons-io"
   }
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs)
    {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) { exclude 'mcmod.info'}
}

jar
{
	from {configurations.compileClasspath.filter {"mixin-${mixin_version}.jar" == it.name}.collect{it.isDirectory() ? it : zipTree(it)}}
	{
		exclude 'dummyThing', 'LICENSE.txt', 'module-info.class', 'META-INF/MANIFSET.MF', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF'
	}
	manifest.attributes (
			'FMLAT': "${mod_id}_at.cfg",
			'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
			'MixinConfigs': "META-INF/${mod_id}_mixin.json",
			'FMLCorePluginContainsFMLMod': 'true'
	)
	finalizedBy 'reobfJar'
}